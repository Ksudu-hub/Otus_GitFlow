#Область ПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ТегПозитив = "Позитив, Губер Ксения";	
	
    ЮТТесты.ДобавитьТестовыйНабор("Документы Приход и Расход товара: Проверка создания, проведения и закрытия регистров").ВТранзакции()
		.ДобавитьТест("ПроверкаСоздания"         ,, ТегПозитив)
		.ДобавитьТест("ПроверкаПроведения"       ,, ТегПозитив)
		.ДобавитьТест("ПроверкаЗакрытияРегистров",, ТегПозитив)
		;

КонецПроцедуры

Процедура ПроверкаСоздания() Экспорт
	
	Для НомерДокумента = 1 По 4 Цикл
		
		// 1. Создание товара.
		ТоварНовый = СозданиеТовара();
		
		// 2. Создание документов.
		Цена       = 5 * НомерДокумента;
		Количество = 3 * НомерДокумента;
		
		ПриходТовара = СозданиеДокументаПриход(ТоварНовый, Цена, Количество);
		РасходТовара = СозданиеДокументаРасход(ПриходТовара, ТоварНовый, Цена, Количество);
		
		// 3. Проверка корректной записи документов.
		ЮТест.ОжидаетЧто(ПриходТовара.ПолучитьОбъект())
			.Метод("Записать").Параметр(РежимЗаписиДокумента.Запись)
			.НеВыбрасываетИсключение()	
			;
		
		ЮТест.ОжидаетЧто(РасходТовара.ПолучитьОбъект())
			.Метод("Записать").Параметр(РежимЗаписиДокумента.Запись)
			.НеВыбрасываетИсключение()	
			;
			
	КонецЦикла;		

КонецПроцедуры

Процедура ПроверкаПроведения() Экспорт
	
	Для НомерДокумента = 1 По 4 Цикл
		
		// 1. Создание товара.
		ТоварНовый = СозданиеТовара();
		
		// 2. Создание документов.
		Цена       = 5 * НомерДокумента;
		Количество = 3 * НомерДокумента;
		
		ПриходТовара = СозданиеДокументаПриход(ТоварНовый, Цена, Количество);
		РасходТовара = СозданиеДокументаРасход(ПриходТовара, ТоварНовый, Цена, Количество);
		
		// 3. Проверка корректной записи документов.
		ЮТест.ОжидаетЧто(ПриходТовара.ПолучитьОбъект())
			.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
			.НеВыбрасываетИсключение()	
			;
		
		ЮТест.ОжидаетЧто(РасходТовара.ПолучитьОбъект())
			.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
			.НеВыбрасываетИсключение()	
			;
			
		// 4. Проверка наличия движений в регистрах.	
		СтруктураДвиженийПриход = Новый Структура("Взаиморасчеты, ТоварныеЗапасы");
		Для Каждого СтрокаДвижение Из СтруктураДвиженийПриход Цикл
			
			ДвиженияПриходТовара = ЮТЗапросы.ДвиженияДокумента(ПриходТовара, СтрокаДвижение.Ключ);
			ЮТест.ОжидаетЧто(ДвиженияПриходТовара)
				.Заполнено()
			; 
			
		КонецЦикла;
		
		СтруктураДвиженийРасход = Новый Структура("Взаиморасчеты, Продажи, ТоварныеЗапасы");
		Для Каждого СтрокаДвижение Из СтруктураДвиженийРасход Цикл
			
			ДвиженияРасходТовара = ЮТЗапросы.ДвиженияДокумента(РасходТовара, СтрокаДвижение.Ключ);
			ЮТест.ОжидаетЧто(ДвиженияРасходТовара)
				.Заполнено()
			; 
			
		КонецЦикла;
			
	КонецЦикла;		

КонецПроцедуры

Процедура ПроверкаЗакрытияРегистров() Экспорт
	
	Для НомерДокумента = 1 По 4 Цикл
		
		// 1. Создание товара
		ТоварНовый = СозданиеТовара();
		
		// 2. Создание документов
		Цена       = 5 * НомерДокумента;
		Количество = 3 * НомерДокумента;
		
		ПриходТовара = СозданиеДокументаПриход(ТоварНовый, Цена, Количество);
		РасходТовара = СозданиеДокументаРасход(ПриходТовара, ТоварНовый, Цена, Количество);
		
		// 3. Проверка корректной записи документов
		ЮТест.ОжидаетЧто(ПриходТовара.ПолучитьОбъект())
			.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
			.НеВыбрасываетИсключение()	
			;
		
		ЮТест.ОжидаетЧто(РасходТовара.ПолучитьОбъект())
			.Метод("Записать").Параметр(РежимЗаписиДокумента.Проведение)
			.НеВыбрасываетИсключение()	
			;
			
		// 4. Проверка, что движения закрываются в ноль
		ОписаниеЗапросВзаиморасчеты = ЗапросПоОстаткамВзаиморасчеты(ПриходТовара.Поставщик, ПриходТовара.Валюта);
	    ЮТест.ОжидаетЧто(ЮТЗапросы.РезультатПустой(ОписаниеЗапросВзаиморасчеты)).ЭтоИстина();
		
		ОписаниеЗапросПоОстаткамТоварныеЗапасы = ЗапросПоОстаткамТоварныеЗапасы(ПриходТовара.Склад, ТоварНовый);
	    ЮТест.ОжидаетЧто(ЮТЗапросы.РезультатПустой(ОписаниеЗапросПоОстаткамТоварныеЗапасы)).ЭтоИстина();

	КонецЦикла;		

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СозданиеТовара()
	
	КонструкторТовара = ЮТест.Данные().КонструкторОбъекта("Справочники.Товары")
		.ФикцияОбязательныхПолей()
		.ФикцияРеквизитов("Наименование")
		.Установить("Вид", Перечисления.ВидыТоваров.Товар)
		;
		
    ТоварНовый = КонструкторТовара.Записать();
	
	Возврат ТоварНовый;

КонецФункции

Функция СозданиеДокументаПриход(ТоварНовый, Цена, Количество)

	// Создание прихода
	ПриходТовара = ЮТест.Данные().КонструкторОбъекта("Документы.ПриходТовара")
		.ФикцияОбязательныхПолей()
		.ФикцияРеквизитов("Поставщик, Склад, Валюта, Организация")
		.Установить("Дата", ТекущаяДатаСеанса() - 100)
		.ТабличнаяЧасть("Товары").ДобавитьСтроку()
			.Установить("Товар"     , ТоварНовый)
			.Установить("Цена"      , Цена)
			.Установить("Количество", Количество)
			.Установить("Сумма"     , Цена * Количество)
		.Записать()
		;
	
	Возврат ПриходТовара;

КонецФункции 

Функция СозданиеДокументаРасход(ПриходТовара, ТоварНовый, Цена, Количество)

	РасходТовара = ЮТест.Данные().КонструкторОбъекта("Документы.РасходТовара")
		.ФикцияОбязательныхПолей()
		.Установить("Дата"       , ТекущаяДатаСеанса())
		.Установить("Склад"      , ПриходТовара.Склад)
		.Установить("Валюта"     , ПриходТовара.Валюта)
		.Установить("Организация", ПриходТовара.Организация)
		.Установить("Покупатель" , ПриходТовара.Поставщик)
		.ФикцияРеквизитов("ВидЦен, ОбоснованиеОтгрузки")
		.ТабличнаяЧасть("Товары").ДобавитьСтроку()
			.Установить("Товар"     , ТоварНовый)
			.Установить("Цена"      , Цена)
			.Установить("Количество", Количество)
			.Установить("Сумма"     , Цена * Количество)
		.Записать()	
		;
	
	Возврат РасходТовара;

КонецФункции 

Функция ЗапросПоОстаткамВзаиморасчеты(Контрагент, Валюта)

	ОписаниеЗапроса = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапроса.ИмяТаблицы = "РегистрНакопления.Взаиморасчеты.Остатки(, Контрагент = &Контрагент И Валюта = &Валюта)";
	ОписаниеЗапроса.ВыбираемыеПоля.Добавить("Контрагент");
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Контрагент", Контрагент);
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Валюта"    , Валюта);
	
	Возврат ОписаниеЗапроса;
	
КонецФункции

Функция ЗапросПоОстаткамТоварныеЗапасы(Склад, Товар)

	ОписаниеЗапроса = ЮТЗапросы.ОписаниеЗапроса();
	ОписаниеЗапроса.ИмяТаблицы = "РегистрНакопления.ТоварныеЗапасы.Остатки(, Склад = &Склад И Товар = &Товар)";
	ОписаниеЗапроса.ВыбираемыеПоля.Добавить("Товар");
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Склад", Склад);
	ОписаниеЗапроса.ЗначенияПараметров.Вставить("Товар", Товар);
	
	Возврат ОписаниеЗапроса;
	
КонецФункции


#КонецОбласти
